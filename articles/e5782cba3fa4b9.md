---
title: "ddu-filerでディレクトリをハイジャックする"
emoji: "🎋"
type: "tech" # tech: 技術記事 / idea: アイデア
topics: ["vim", "denops"]
published: false
---

# 注意書き
筆者はVimを学生時代から使っていますがいまだに浅いところで生活している人間で、記事を書くこと自体も慣れていないよわよわなのでそれを前提に少しでも楽しんでいただけたら嬉しいです。

# はじめに
あけましておめでとうございます。
みなさん、netrwは使ってますか？

本当に残念ですが、僕は使っていないです
一部のnetrwカスタマイズ猛者の方でなければnetrw自体を拡張するんじゃなくて、以下の理由で使わない選択をすると思います。

- ディレクトリは空でなければ削除できない
- コピーにはマーク(ファイル選択と同じ:h netrw-mf)をする必要があり、プロンプトを介してできない
- ディレクトリはコピーできない
- 一度に複数ファイルを移動・コピーできない

ぺりーさんの[netrwを使うために](https://satorunooshie.medium.com/netrw-db43a2674086)から引用

（本当に申し訳ない）あまりよく知らないんですが一年前くらいにメンテナが現れてリポジトリができてたっぽい？
https://www.reddit.com/r/vim/comments/1i409up/netrw_now_has_a_new_maintainer_and_repo
https://github.com/saccarosium/netrw.vim
ちゃんと追いきれていないのであまり参考にしてほしくはないのですがRepository自体は閉じられたけどバグ修正や安全性改善は進んでいそう？
ただ、UXとかは改善されていないっぽいので使う理由にはならないかもですね。（Vimは後方互換のためにnetrw剥がさないと思うんですがnetrwが使えるとVimを薄く使うことができるので嬉しいという需要はある）

使わない人はddu-filer, oil.nvim, vim-fernなどを使うと思うのですがoil.nvimやvim-fernのように簡単にNetrwをハイジャックしてくれるものもあれば自作のファイラやddu-filerなど設定してくれないものもあると思います。自分はVimでShougoさんのプラグインを最近使用することが多いのですがddu-filerを使っています。めっちゃ遠回りしてしまったのですが、今回は簡単にNetrwをハイジャックしてくれないものに関して自分の一例を紹介したいと思います。

# コード
参考実装 - [vim-fern-hijack](https://github.com/lambdalisue/vim-fern-hijack/blob/master/plugin/fern_hijack.vim)
```vim
if exists('g:loaded_ddu_filer_hijack')
  finish
endif
let g:loaded_ddu_filer_hijack = 1


function! s:suppress_netrw() abort
  if exists('#FileExplorer')
    autocmd! FileExplorer *
  endif
endfunction


function! s:hijack_bufenter() abort
  let l:path = expand('%:p')
  if empty(l:path) || !isdirectory(l:path)
    return
  endif

  let l:bufnr = bufnr()
  call ddu#start({ 'name': 'filer', 'sourceOptions': { '_': { 'path': l:path } } })
  silent! execute 'bwipeout' l:bufnr
endfunction


function! s:hijack_argv() abort
  if argc() != 1
    return
  endif

  let l:path = fnamemodify(argv(0), ':p')
  if !isdirectory(l:path)
    return
  endif

  call ddu#start({ 'name': 'filer', 'sourceOptions': { '_': { 'path': l:path } } })
  silent! bwipeout!
endfunction


augroup ddu-filer-hijack
  autocmd!
  autocmd VimEnter * call s:suppress_netrw()
  autocmd VimEnter * call s:hijack_argv()
  autocmd BufEnter * ++nested call s:hijack_bufenter()
augroup END
```

# 最後に

# 余談
denops.vimの特性上どうしても初回のプロセスの起動が遅くなってしまうため`vim .`や`vim directory/`で起動した時に遅延が発生してしまいます。[shared-server](https://github.com/vim-denops/denops-shared-server.vim)などを使用することによって緩和することはできますが、相性が悪いのは事実です。ですが自分はddu-ui-filerを愛用しているため今回記録を残すという意味合いも込めて記事を書かせていただきました。
もし間違ったところ、こうする方がいいよなどあればコメントなどで言ってもらえると幸いです！！

# 参考にした記事など
- [netrwを使うために](https://satorunooshie.medium.com/netrw-db43a2674086)
